plugins {
    id 'java'
    id 'application'
    id 'edu.sc.seis.launch4j' version '2.4.4'
    id 'edu.sc.seis.macAppBundle' version '2.3.0'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'

    compile 'com.drewnoakes:metadata-extractor:2.11.0'
    compile 'org.slf4j:slf4j-api:1.8.0-beta4'
    compile 'org.slf4j:slf4j-log4j12:1.8.0-beta4'
    compile 'log4j:log4j:1.2.17'
}

sourceCompatibility = 1.8
mainClassName = 'com.williamquast.Application'
group 'com.williamquast'
version '1.0'

// for start script dist
distZip {
    archiveName "${archivesBaseName}-${project.version}-bin.zip"
}
distTar.enabled = false

// for windows dist
launch4j {
    mainClassName = 'com.williamquast.Application'
    icon = "${projectDir}/src/main/resources/app.ico"
    headerType = 'gui'
}

task createWindowsDist(type: Zip) {
    dependsOn(createAllExecutables)
    from "${project.buildDir}/launch4j"
    into "${archivesBaseName}"
    include '**/*'
    archiveName "${archivesBaseName}-${project.version}-win.zip"
    destinationDir(file("${project.buildDir}/distributions/"))
}
assemble.dependsOn(createWindowsDist) // assemble includes windows

// for mac dist
macAppBundle {
    mainClassName = 'com.williamquast.Application'
    icon = "${projectDir}/src/main/resources/app.ico"
    dmgName = "${archivesBaseName}-${project.version}-mac"
    javaProperties.put("apple.laf.useScreenMenuBar", "true")
}
assemble.dependsOn(createAppZip) // assemble includes mac
