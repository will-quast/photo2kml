plugins {
    id 'java'
    id 'edu.sc.seis.launch4j' version '2.4.4'
    id 'edu.sc.seis.macAppBundle' version "2.3.0"
}

group 'com.williamquast'
version '0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile 'com.drewnoakes:metadata-extractor:2.11.0'
}

// for windows dist
launch4j {
    mainClassName = 'com.williamquast.Application'
    icon = "${projectDir}/src/main/resources/app.ico"
    headerType = 'gui'
}

task createWindowsDist(type: Zip) {
    dependsOn(createAllExecutables)
    from "${project.buildDir}/launch4j"
    include '**/*'
    archiveName "${archivesBaseName}-win-${project.version}.zip"
    destinationDir(file("${project.buildDir}/distributions/"))
}
assemble.dependsOn(createWindowsDist) // assemble includes windows

// for mac dist
macAppBundle {
    mainClassName = 'com.williamquast.Application'
    icon = "${projectDir}/src/main/resources/app.ico"
    dmgName = "${archivesBaseName}-mac-${project.version}"
    javaProperties.put("apple.laf.useScreenMenuBar", "true")
}
assemble.dependsOn(createAppZip) // assemble includes mac
